apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

def keystorePropertiesFile = rootProject.file("signinfo.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    // https://blog.csdn.net/qq_27258799/article/details/77259129
    signingConfigs {
        debug {
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
            storeFile file(keystoreProperties['debugStoreFile'])
            storePassword keystoreProperties['debugStorePassword']
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.csii.mobilebank"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "3.062"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // https://developer.android.com/studio/build/multidex.html?hl=zh-cn#keep
        // 由于存在 Dalvik linearAlloc 限制（问题 78035），因此，如果使用 Dalvik 可执行文件分包配置的应用发出非常庞大的内存分配请求，则可能会在运行期间发生崩溃。尽管 Android 4.0（API 级别 14）提高了分配限制，但在 Android 5.0（API 级别 21）之前的 Android 版本上，应用仍有可能遭遇这一限制。
        multiDexEnabled true

        ndk {
            //选择要添加的对应 cpu 类型的.so 库。
            abiFilters 'armeabi', 'armeabi-v7a', 'armeabi-v8a', 'x86', 'x86_64', 'mips', 'mips64'
        }

    }
    productFlavors {
    }
    dexOptions {
        additionalParameters = ["--set-max-idx-number=62000"]
        jumboMode true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            // https://blog.csdn.net/qq_27258799/article/details/77259129
            // ！！下面这个不能设置，会导致第三方配置需要包名的地方出错
//            applicationIdSuffix ".release"
        }
        debug {
            signingConfig signingConfigs.debug
//            applicationIdSuffix ".debug"
        }
    }

    configurations.all {
        resolutionStrategy {
            force 'com.android.support:support-compat:27.1.1'
            force 'com.android.support:support-v4:27.1.1'
        }
    }
}

ext.ANDROID_SUPPORT_LIBRARY_VERSION = "27.0.2"
ext.BUTTERKNIFE_VERSION = "9.0.0-SNAPSHOT"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation 'junit:junit:4.12'

    api 'com.android.support:multidex:1.0.3'

    api project(':vplus-ui')

    api 'com.jakewharton:butterknife:'  + BUTTERKNIFE_VERSION
    annotationProcessor 'com.jakewharton:butterknife-compiler:' + BUTTERKNIFE_VERSION
}
