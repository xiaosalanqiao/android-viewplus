buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://maven.google.com' }
        maven { url "https://jitpack.io" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://dl.bintray.com/vplus/android-viewplus" }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        // ButterKnife https://github.com/JakeWharton/butterknife/issues/963
        classpath 'com.jakewharton:butterknife-gradle-plugin:9.0.0-rc1'
        // https://www.jianshu.com/p/4cdabe4d605a
        classpath 'com.novoda:bintray-release:0.8.1'
    }
}

// https://github.com/facebook/react-native/issues/19239
subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains('multidex') ) {
                details.useVersion "27.1.1"
            }
        }
    }
    afterEvaluate {
        android {
            compileSdkVersion 27
            buildToolsVersion "27.0.3"

            defaultConfig {
                targetSdkVersion 27
            }
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://maven.google.com' }
        maven { url "https://jitpack.io" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://dl.bintray.com/vplus/android-viewplus" }
    }
    tasks.withType(Javadoc) {
        options {
            encoding "UTF-8"
            charSet 'UTF-8'
            links "http://docs.oracle.com/javase/8/docs/api"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}